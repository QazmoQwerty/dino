import "Std"

namespace BrainF {
    
    char[30000] tape

    void Interpret(string input) {
        int ptr ≡ 0
        for int i ≡ 0 | i < input.Size | i+=1 {
            char curr ≡ input.Get(i)
            if      curr = '>': ptr += 1
            else if curr = '<': ptr -= 1
            else if curr = '+': tape[ptr] += 1 as char
            else if curr = '-': tape[ptr] -= 1 as char
            else if curr = '.': Std.PrintC(tape[ptr])
            else if curr = ',': tape[ptr] ≡ Std.GetChar()
            else if curr = ']' and tape[ptr] ≠ 0 as char {
                int loop ≡ 1
                while loop > 0 {
                    curr ≡ input.Get(i-=1)
                    if curr =  '[':
                        loop -= 1
                    else if curr = ']':
                        loop += 1
                }
            }
        }
    }

    void Main() {
        for int i ≡ 0 | i < tape.Size | i+=1:
            tape[i] ≡ null

        // outputs input    
        // Interpret(",[.[-],]")

        // outputs the ascii charset
        // Interpret(".+[.+]")

        // Hello World!
        Interpret("++++++++[>++++[>++>+++>+++>+<<<<-]>+>+>->>+[<]<-]>>.>---.+++++++..+++.>>.<-.<.+++.------.--------.>>+.>++.")
    }
}